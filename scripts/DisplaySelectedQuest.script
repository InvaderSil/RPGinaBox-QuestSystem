// Param: $questObject -> is a codex holding the quest id and the 
if ($questObject == null) then
	print("invalid questObject");
	return -1;
end;

$questId = $questObject["questId"];
$selectedImageId = $questObject["checkImageId"];
$questDetailsElemntId = "0001";
$abaondonElementId = "0005";
$questWidgetId = "Quest2";

$questCodex = global.property["ActiveQuestList"];

if $questCodex[$questId] == null then
	print($questId + " was not found.");
	return -1;
end;

// First calculate the percert done on the quest
$stepsArray = $questCodex[$questId]["steps"];
$totalCount = $stepsArray.size();
$completedCount = 0;
for $i in $stepsArray do
	if $i then
		$completedCount += 1;
	end;
end;
global.property["SelectedQuestMax"] = $totalCount;
global.property["SelectedQuestValue"] = $completedCount;

// Set the quest details
$textDescription = "Title: " + str($questCodex[$questId]["title"]);
$textDescription = $textDescription + "\nDescription:\n " + str($questCodex[$questId]["description"]);
widget[$questWidgetId].element[$questDetailsElemntId].text = $textDescription;

// And finally, set the abandon button
widget[$questWidgetId].element[$abaondonElementId].script = $questCodex[$questId]["abandonScript"];

// no, this is finally. Enable the check box image next to the selected button
$buttonData = global.property["QuestLogButtons"];
for $button in $buttonData do
	$checkButtonId = $button["checkImageId"];
	widget[$questWidgetId].element[$checkButtonId].visible = false;
end;

widget[$questWidgetId].element[$selectedImageId].visible = true;
