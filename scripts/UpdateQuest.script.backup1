// To be called from an execute_script
// params: 	$questId -> string: the quest id as found in the quest editor
// 			$stepNumber -> number: essentially the index of the step
if $questId == null then
	print("invalid questId");
	return -1;
end;

if $stepNumber == null then
	print("invalid step number");
	return -1;
end;

// first get the quest from the active quest list
$questArray = global.property["ActiveQuestList"];
$isFound = false;
$result = "";
$index = 0;
for $localQuest in $questArray do
	if $localQuest["questID"] == $questId and $localQuest["isFinished"] == false then
		$isFound = true;
		print("questId -> " + $questId + " is found!");
		$result = $localQuest;
		break;
	end;
	$index += 1;
end;

if $isFound == false then
	print($questId + " was not found.");
	return -1;
end;

// Update the numstep to true
$questArray[$index]["steps"][$stepNumber] = true;

$procStep = $questArray[$index]["steps"];
// Validate that all steps are done
if $procStep contains false then
	print("Quest still in progress.");
else
	$questArray[$index]["isFinished"] = true;
 	complete_quest($questId);
end;



