$questWidgetName = "Quest2";
$questWidget = widget[$questWidgetName];

$buttonList = global.property["QuestLogButtons"];

$questCodex = global.property["ActiveQuestList"];

// Getting list of keys from the codex
$questList = array[];
for $key in $questCodex do
	$locQuest = $questCodex[$key];
	if($locQuest["questStatus"] != "Completed" and $locQuest["questStatus"] != "Abandoned") then
		$questList.push_back($key);
	end;
end;

// hide all the buttons by default
for $but in $buttonList do
	$visButton = $but["buttonId"];
	$questWidget.element[$visButton].visible = false;
end;

$buttonIndex = 0;
for $questKey in $questList do
	$button = $buttonList[$buttonIndex]["buttonId"];
	
	$questWidget.element[$button].visible = true;
	$questWidget.element[$button].text = $questCodex[$questKey]["title"];
	$questWidget.element[$button].script = $questCodex[$questKey]["logScript"];
		
	$buttonIndex += 1;
	if($buttonIndex >= $questList.size()) then
		// we don't want to exceed the number of possible buttons
		break;
	end;
end;

// populate the selected quest for the first quest in the list if there is one
if $questList.size() > 0 then
	$questObject = codex["questId": $questCodex[$questList[0]]["questID"], "checkImageId": $buttonList[0]["checkImageId"]];
	execute_script("DisplaySelectedQuest", true);
	show_widget($questWidgetName);
else
	hide_widget($questWidgetName);
	display_message("You have no active quests.");
end;
